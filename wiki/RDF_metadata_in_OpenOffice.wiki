#summary how to use rdf metadata in openoffice

<wiki:toc max_depth="3" />

= Introduction = 

Since OpenOffice.org 3.3 it is possible to apply RDF metadata on ODF elements.
There is API support for this [http://wiki.services.openoffice.org/wiki/Documentation/DevGuide/OfficeDev/RDF_metadata RDF Metadata], but the documentation is very scanty on details. This page attempts to document the API using examples.

== Apply simple RDF comment on a section == 

This example applies a RDF Comment on the contents of a section called "Section1" which contains some text.

{{{
Sub Main

  Dim odoc
  odoc = thisComponent

  Dim osection 
  osection = odoc.getTextSections().getByName("Section1")

  Dim sectionRange
  sectionRange = osection.getAnchor()

  Dim docText
  docText= odoc.getText()

  'Apply an instance of InContentMetadata to the contents of the section
  Dim icmeta
  icmeta = odoc.createInstance("com.sun.star.text.InContentMetadata")
  docText.insertTextContent(sectionRange, icmeta, true)

  UriService = com.sun.star.rdf.URI
  UriSService = com.sun.star.rdf.URIs
  LiteralService = com.sun.star.rdf.Literal

  'Generate a RDF comment
  Dim uriComment
  uriComment = UriService.createKnown(UriSService.RDFS_COMMENT)
  Dim rdfLiteral
  rdfLiteral =LiteralService.create("Simple RDF Comment")

  'The RDF comment is captured in RDF metadata file
  Dim rdfDoc 
  rdfDoc = UriService.create( "http://akomantoso.org/ontologies/section/KE/ontology")
  
  Dim rdfMetafile
  rdfMetafile = odoc.addMetadataFile("akn/akn.rdf", Array(rdfDoc))

  'Get a RDF graph for the RDF metadata file 
  Dim rdfGraph
  rdfGraph = odoc.getRDFRepository().getGraph(rdfMetafile)
  'Add the metadata comment statement
  rdfGraph.addStatement(icmeta, uriComment, rdfLiteral)

  ' You will need to save the document manual after running this

End Sub
}}}

After saving the document -- examine the internals of of the ODF file.
The metadata is captured in a file component `akn/akn.rdf` inside the ODF file :

{{{
<?xml version="1.0" encoding="utf-8"?>
<rdf:RDF xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#">
  <rdf:Description rdf:about="../content.xml#id862116040">
    <ns1:comment xmlns:ns1="http://www.w3.org/2000/01/rdf-schema#">Simple RDF Comment</ns1:comment>
  </rdf:Description>
</rdf:RDF>
}}}

The identifer in `rdf:Description/@rdf:about` ...

{{{
<rdf:Description rdf:about="../content.xml#id862116040">
}}}

... is the identifier  of the fragment of text in `Section1`

{{{

<text:section text:style-name="Sect1" text:name="Section1">
   <text:p text:style-name="Standard">
	<text:meta xml:id="id1200662435">This is some text</text:meta> <=== 
   </text:p>
</text:section>
}}}

== Apply metadata on a section ==

Metadata can also be applied directly on the section as a whole.
In this example -- we add metadata triples in a foreign namespace to the section rdf metadata :

{{{
 Dim odoc, osection, otext
 odoc = ThisComponent
 osection = odoc.getTextSections().getByName("Section1")
 otext = odoc.getText()
 
 UriService = com.sun.star.rdf.URI
 UrisService = com.sun.star.rdf.URIs
 LiteralService = com.sun.star.rdf.Literal
 
 Dim uriAnx, rdfMetafile, rdfGraph 
 uriAnx = UriService.create("http://editor.bungeni.org/anx")
 rdfMetafile = odoc.addMetadataFile("meta/meta.rdf", Array(uriAnx))
 rdfGraph = odoc.getRDFRepository().getGraph(rdfMetafile)
 
 Dim uriType, litType
 uriType = UriService.create("http://editor.bungeni.org/anx/Type")
 litType = LiteralService.create("Clause")
 
 Dim uriUuid, litUuid
 uriUuid = UriService.create("http://editor.bungeni.org/anx/UUID")
 litUuid = LiteralService.create("1234-3876-5150-3615")
 
 rdfGraph.addStatement(osection, uriType, litType)
 rdfGraph.addStatement(osection, uriUuid, litUuid)
}}}

The rdf statements appear like this :

{{{
<?xml version="1.0" encoding="utf-8"?>
<rdf:RDF xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#">
  <rdf:Description rdf:about="../content.xml#id1269007312">
    <ns1:Type xmlns:ns1="http://editor.bungeni.org/anx/">Clause</ns1:Type>
    <ns2:UUID xmlns:ns2="http://editor.bungeni.org/anx/">1234-3876-5150-3615</ns2:UUID>
  </rdf:Description>
</rdf:RDF>

}}}